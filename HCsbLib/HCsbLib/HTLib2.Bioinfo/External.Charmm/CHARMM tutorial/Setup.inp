* Run Segment Through CHARMM
*

! read topology and parameter files

read rtf card name top_all27_prot_na.rtf
read param card name par_all27_prot_na.prm

! Read sequence from the PDB coordinate file
open unit 1 card read name 1cbn-a.pdb
read sequ pdb unit 1

! now generate the PSF and also the IC table (SETU keyword)
generate setu a-pro first NTER last CTER

rewind unit 1

! set bomlev to -1 to avois sying on lack of hydrogen coordinates
bomlev -1
read coor pdb unit 1
! them put bomlev back up to 0
bomlev 0

close unit 1

! prints out number of atoms that still have undefined coordinates.
define test select segid a-pro .and. ( .not. hydrogen ) .and. ( .not. init ) show end

ic para
ic fill preserve
ic build
hbuild sele all end

! write out the protein structure file (psf) and
! the coordinate file in pdb and crd format.

write psf card name 1cbn-pro-final.psf
* PSF
*

write coor card name 1cbn-pro-final.crd
* Coords
*

stop

