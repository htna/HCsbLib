* potassiums and chloride addition; @ineg CL- @ipos K+ MIN DIST @MND
* excluding @SOL
*

! sol   particles to exclude; atom selection w/o SELE and END
! ineg   no. of chlorides
! ipos   no. of potassiums
! mnd   min distance to another solute particle

! INITIALIZE CHLORIDES; COORDS SET 0,0,0
read sequ @nneg @ineg
generate @negion noang nodihe first none last none
coor set xdir 0.0 ydir 0.0 zdir 0.0 sele segid @negion end

! PICK A WATER MOLECULE AND REPLACE IT WITH A CHLORIDE ION
set k 1
label ilp
   define xcld sele ( @SOL ) .or. segid @negion end                     ! EXCLUDED SEGMENTS
   define prox sele xcld .around. @MND end                              ! NEARBY EXCLUDED ATOMS
   define list sele atom BWAT * OH2 .and. .not. prox end                ! WATERS NOT NEARBY
   calc in int( ?RAND * ?NSEL )                                         ! RANDOM INTEGER, BASED ON WATER COUNT
   if in .lt. 1 set in ?NSEL                                            ! CHECK FOR ZERO, CHANGE TO MAX VALUE
   define targ sele list .subset. @IN end                               ! PICK WATER VIA RANDOM INDEX
   coor stat sele targ end                                              ! GET OH2 ATOM COORDS, ASSIGN TO CHLORIDE
   coor set xdir ?XAVE ydir ?YAVE zdir ?ZAVE sele atom @negion @K @nneg end
   delete atom sele .byres. targ end sort                               ! REMOVE THE WATER MOLECULE
   incr k by 1
if k .le. @ineg goto ilp

! INITIALIZE POTASSIUMS, REPEAT ABOVE PROCESS
read sequ @npos @ipos
generate @posion noang nodihe first none last none
coor set xdir 0.0 ydir 0.0 zdir 0.0 sele segid @posion end

! NOTE THAT BOTH SEGID CL AND SEGID K ARE 'EXCLUDED'
! NEARBY WATERS ARE REALLY EXCLUDED; REPLACE ONLY 'BULK' WATERS
set k 1
label klp
   define xcld sele ( @SOL ) .or. segid @negion .or. segid @posion end
   define prox sele xcld .around. @MND end
   define list sele atom BWAT * OH2 .and. .not. prox end
   calc in int( ?RAND * ?NSEL )
   if in .lt. 1 set in ?NSEL
   define targ sele list .subset. @IN end
   coor stat sele targ end
   coor set xdir ?XAVE ydir ?YAVE zdir ?ZAVE sele atom @posion @K @npos end
   delete atom sele .byres. targ end sort
   incr k by 1
if k .le. @ipos goto klp

return

